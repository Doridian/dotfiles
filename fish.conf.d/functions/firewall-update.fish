function _iptables_header
    echo '# Generated by firewall-update.fish'
    echo '*filter'
    echo ':INPUT DROP [0:0]'
    echo ':FORWARD DROP [0:0]'
    echo ':OUTPUT ACCEPT [0:0]'
    echo '-A INPUT -i lo -j ACCEPT'
    echo '-A INPUT -p tcp -m tcp --dport 22 -j ACCEPT' # SSH
    echo '-A INPUT -p udp -m udp --sport 5353 --dport 5353 -j ACCEPT' # mDNS
end

function _ip4tables_header
    _iptables_header
    echo '-A INPUT -p icmp -j ACCEPT'
    echo '-A INPUT -p udp -m udp --sport 67 --dport 68 -j ACCEPT' # DHCP
end

function _ip6tables_header
    _iptables_header
    echo '-A INPUT -p ipv6-icmp -j ACCEPT'
    echo '-A INPUT -p udp -m udp --sport 547 --dport 546 -j ACCEPT' # DHCPv6
end

function _iptables_footer
    echo '-A INPUT -m conntrack --ctstate RELATED,ESTABLISHED -j ACCEPT'
    echo '-A INPUT -p tcp -j REJECT --reject-with tcp-reset'
end

function _ip4tables_footer
    _iptables_footer
    echo '-A INPUT -p udp -j REJECT --reject-with icmp-admin-prohibited'
    echo '-A INPUT -j REJECT --reject-with icmp-admin-prohibited'
    echo 'COMMIT'
end

function _ip6tables_footer
   _iptables_footer
    echo '-A INPUT -p udp -j REJECT --reject-with icmp6-adm-prohibited'
    echo '-A INPUT -j REJECT --reject-with icmp6-adm-prohibited'
    echo 'COMMIT'
end

function _iptables_port -a proto port
    echo "-A INPUT -p $proto -m $proto --dport $port -j ACCEPT"
end

function firewall-update
	set -l open_ports_tcp 666 22000
	set -l open_ports_udp 22000

    set -l iptables_dir "$_dotfiles_root_dir/../system/iptables/"
    set -l iptables_system_dir "/etc/iptables/"
    set -l iptables_path "$iptables_dir/iptables.rules"
    set -l ip6tables_path "$iptables_dir/ip6tables.rules"

    # IPv4
    echo "Updating $iptables_path and $ip6tables_path"
    _ip4tables_header > $iptables_path
    _ip6tables_header > $ip6tables_path
    for port in $open_ports_tcp
        set -l line (_iptables_port tcp $port)
        echo $line >> $iptables_path
        echo $line >> $ip6tables_path
    end
    for port in $open_ports_udp
        set -l line (_iptables_port udp $port)
        echo $line >> $iptables_path
        echo $line >> $ip6tables_path
    end
    _ip4tables_footer >> $iptables_path
    _ip6tables_footer >> $ip6tables_path

    sudo bash -c "cp -fv $iptables_dir/*.rules $iptables_system_dir && systemctl restart iptables && systemctl restart ip6tables"
end
